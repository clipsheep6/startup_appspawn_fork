# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//base/startup/init_lite/begetd.gni")
import("//build/ohos.gni")

config("exported_header_files") {
  visibility = [ ":*" ]
  include_dirs = [
    "//base/startup/init_lite/interfaces/innerkits/include",
    "//base/startup/init_lite/services/include/param",
    "//base/startup/init_lite/services/include",
    "//base/startup/init_lite/services/loopevent/include",
    "//base/startup/init_lite/services/log",
  ]
}

base_sources = [
  "//base/startup/init_lite/services/log/init_commlog.c",
  "//base/startup/init_lite/services/param/base/param_base.c",
  "//base/startup/init_lite/services/param/base/param_comm.c",
  "//base/startup/init_lite/services/param/base/param_trie.c",
  "//base/startup/init_lite/services/utils/init_hashmap.c",
  "//base/startup/init_lite/services/utils/list.c",
]

base_include_dirs = [
  "//base/startup/init_lite/services/param/include",
  "//base/startup/init_lite/services/param/adapter",
  "//base/startup/init_lite/services/param/linux",
  "//base/startup/init_lite/services/param/base",
  "//base/security/selinux/interfaces/policycoreutils/include",
]

if (defined(ohos_lite)) {
  static_library("parameterbase") {
    sources = base_sources
    include_dirs = base_include_dirs
    cflags = [ "-fPIC" ]
    defines = [ "_GNU_SOURCE" ]
    public_configs = [ ":exported_header_files" ]

    if (ohos_kernel_type == "linux") {
      sources += [
        "//base/startup/init_lite/services/param/adapter/param_dac.c",
        "//base/startup/init_lite/services/param/linux/param_osadp.c",
      ]
      defines += [
        "__LINUX__",
        "PARAM_BASE",
      ]
    } else if (ohos_kernel_type == "liteos_a") {
      sources += [
        "//base/startup/init_lite/services/param/liteos/param_litedac.c",
        "//base/startup/init_lite/services/param/liteos/param_osadp.c",
      ]
      include_dirs += [ "//third_party/bounds_checking_function/include" ]
      defines += [
        "__LITEOS_A__",
        "WORKSPACE_AREA_NEED_MUTEX",
        "PARAM_PERSIST_SAVE_MUTEX",
        "PARAMWORKSPACE_NEED_MUTEX",
      ]
    }
  }
} else {
  ohos_static_library("parameterbase") {
    sources = base_sources
    sources += [
      "//base/startup/init_lite/services/param/adapter/param_dac.c",
      "//base/startup/init_lite/services/param/linux/param_osadp.c",
    ]
    cflags = [ "-fPIC" ]
    include_dirs = base_include_dirs
    public_configs = [ ":exported_header_files" ]
    defines = [ "_GNU_SOURCE" ]

    if (use_musl) {
      defines += [ "PARAM_BASE" ]
    } else {
      include_dirs += [ "//third_party/bounds_checking_function/include" ]
    }
    if (build_selinux) {
      include_dirs += [
        "//third_party/selinux/libselinux/include/",
        "//base/security/selinux/interfaces/policycoreutils/include/",
      ]
      sources +=
          [ "//base/startup/init_lite/services/param/adapter/param_selinux.c" ]
      defines += [
        "PARAM_SUPPORT_SELINUX",
        "PARAMWORKSPACE_NEED_MUTEX",
      ]
    }
    part_name = "init"
  }
}
